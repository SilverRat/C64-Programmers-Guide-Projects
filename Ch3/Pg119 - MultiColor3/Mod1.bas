10 REM * EXAMPLE 2 *
20 REM CREATING MULTI COLOR PROGRAMMABLE CHARACTERS
30 POKE56334,PEEK(56334)AND254:POKE1,PEEK(1)AND251
40 REM CHARACTER RANGE TO BE COPIED FROM ROM
50 FORI=0TO63
60 REM COPY ALL 8 BYTES PER CHARACTER
70 FORJ=0TO7
80 REM COPY A BYTE
90 POKE12288+I*8+J,PEEK(53248+I*8+J)
100 REM GOTO NEXT BYTE OR CHARACTER
110 NEXT J,I
120 REM TURN ON I/O AND KEYBOARD
130 POKE1,PEEK(1)OR4:POKE56334,PEEK(56334)OR1
140 REM SET CHAR POINTER TO MEM 12288
150 POKE53272,(PEEK(53272)AND240)+12
160 POKE53270,PEEK(53270)OR16
170 REM SET BACKGROUND COLOR #0 TO BLACK
180 POKE53281,0
190 REM SET BACKGROUND COLOR #1 TO RED
200 POKE53282,2
210 REM SET BACKGROUND COLOR #2 TO YELLOW
220 POKE53283,7
230 REM PROGRAM CHARACTERS 60 THRU 63
240 FORC=60TO63
250 REM DO ALL 8 BYTES OF A CHARACTER
260 FORB=0TO7
270 REM READ 1/8TH OF A CHARACTER DATA
280 READNUMBER
290 REM STORE THE DATA IN MEMORY
300 POKE12288+(8*C)+B,NUMBER
310 NEXTB,C
320 REM PUT THE NEWLY DEFINED CHARACTERS ON THE SCREEN
330 PRINT"{clear}"TAB(255)CHR$(60)CHR$(61)TAB(55)CHR$(62)CHR$(63)
340 REM WAIT FOR USER TO PRESS A KEY
345 GETA$
348 REM IF NO KEY PRESSED TRY AGAIN
350 IFA$=""THEN345
355 REM RETURN TO NORMAL CHARACTERS
360 POKE53272,21:POKE53270,PEEK(53270)AND239
370 REM DATA FOR CHARACTER 60
380 DATA129,37,21,29,93,85,85,85
390 REM DATA FOR CHARACTER 61
400 DATA66,72,84,116,117,85,85,85
410 REM DATA FOR CHARACTER 62
420 DATA87,87,85,21,8,8,40,0
430 REM DATA FOR CHARACTER 63
440 DATA213,213,85,84,32,32,40,0
450 END