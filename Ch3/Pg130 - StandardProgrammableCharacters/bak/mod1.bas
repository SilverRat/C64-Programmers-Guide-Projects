10 REM * EXAMPLE 1 *
20 REM CREATING PROGRAMMABLE CHARACTERS
25 REM TURN OFF KEYBOARD AND IO
30 POKE56334,PEEK(56334)AND254:POKE1,PEEK(1)AND251
45 REM CHARACTER RANGE TO BE COPIED FROM ROM
48 REM THOUGH, COPIED CHARS NOT USED IN THIS EXAMPLE
50 FORI=0TO63
60 REM COPY ALL 8 BYTES PER CHARACTER
70 FORJ=0TO7
80 REM COPY A BYTE 
90 POKE12288+I*8+J,PEEK(53248+I*8+J)
100 NEXTJ:NEXTI
110 REM TURN ON KEYBOARD AND IO
120 POKE1,PEEK(1)OR4:POKE56334,PEEK(56334)OR1
130 REM SET CHARACTER POINTER TO MEMORY 12288
135 POKE53272,(PEEK(53272)AND240)+12
140 REM PROGRAM CHARACTERS 60-63
150 FORC=60TO63
160 REM DO ALL 8 BYTES FOR EACH CHARACTER
170 FORB=0TO7
180 REM READ IN 1/8TH OF CHARACTER DATA
190 READ NUMBER
200 REM STORE THAT DATA IN RAM MEMORY
210 POKE12288+(8*C)+B,NUMBER
220 NEXTB:NEXTC
230 REM PUT NEW CHARACTERS ON THE SCREEN
240 PRINTCHR$(147)TAB(255)CHR$(60)CHR$(61)
250 PRINTTAB(15)CHR$(62)CHR$(63)
260 REM WAIT FOR A CHARACTER PRESS
270 GET A$
280 REM IF NO KEYPRESS, TRY AGAIN
290 IFA$="" THEN GOTO270
300 REM RETURN TO NORMAL CHARACTERS
310 POKE53272,21
320 REM DATA FOR CHARACTER 60
330 DATA 4,6,7,5,7,7,3,3
340 REM DATA FOR CHARACTER 61
350 DATA 32,96,224,160,224,224,192,192
360 REM DATA FOR CHARACTER 62
370 DATA 7,7,7,31,31,95,143,127
380 REM DATA FOR CHARACTER 63
390 DATA 224,224,224,248,248,248,240,224
